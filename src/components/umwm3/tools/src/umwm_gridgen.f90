!
! University of Miami Wave Model (UMWM)
! umwm_gridgen
! Copyright (C) 2012  Milan Curcic
!
! This program is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <http://www.gnu.org/licenses/>.
!
!======================================================================!
PROGRAM umwm_gridgen
!=======================================================================
!
! DESCRIPTION: Utility program that generates a simple lat-lon grid
! based on input parameters in namelists/gridgen.nml.
!              
! REVISION HISTORY:
!              
! June 2012, 1.0.1, First public release
! 
!=======================================================================
USE netcdf
USE umwm_utils,ONLY:nc_check
!=======================================================================
IMPLICIT NONE

INTEGER :: i,j
INTEGER :: idm,jdm
INTEGER :: ncid,xdimid,ydimid,lonid,latid

REAL :: dlon,dlat,lon0,lat0

REAL,DIMENSION(:,:),ALLOCATABLE :: lon,lat

LOGICAL :: namelistOK

NAMELIST /gridgen/ idm,jdm,dlon,dlat,lon0,lat0

!=======================================================================

WRITE(*,*)'umwm_gridgen: Reading namelists/gridgen.nml'

! 1) Read namelist:
OPEN(UNIT=10,FILE='namelists/gridgen.nml',STATUS='OLD',&
     FORM='FORMATTED',ACCESS='SEQUENTIAL')
READ(UNIT=10,NML=gridgen)
CLOSE(UNIT=10)

namelistOK = .TRUE.

! Check namelist values:
IF(idm<=0)THEN
  WRITE(*,*)'umwm_gridgen: ERROR: idm must be > 0'
  namelistOK = .FALSE.
ENDIF

IF(jdm<=0)THEN
  WRITE(*,*)'umwm_gridgen: ERROR: jdm must be > 0'
  namelistOK = .FALSE.
ENDIF

IF(dlon<=0)THEN
  WRITE(*,*)'umwm_gridgen: ERROR: dlon must be > 0'
  namelistOK = .FALSE.
ENDIF

IF(dlat<=0)THEN
  WRITE(*,*)'umwm_gridgen: ERROR: dlat must be > 0'
  namelistOK = .FALSE.
ENDIF

IF(lon0<-180.AND.lon0>180.)THEN
  WRITE(*,*)'umwm_gridgen: ERROR: lon0 must be within [-180,180]'
  namelistOK = .FALSE.
ENDIF

IF(lat0<-90.AND.lat0>90.)THEN
  WRITE(*,*)'umwm_gridgen: ERROR: lat0 must be within (-90,90)'
  namelistOK = .FALSE.
ENDIF

IF(.NOT.namelistOK)THEN
  WRITE(*,*)'umwm_gridgen: ABORT: One or more errors encountered &
in namelists/gridgen.nml'
  STOP
ENDIF

!=======================================================================

! 2) Generate grid:

WRITE(*,*)'umwm_gridgen: Generating grid'

ALLOCATE(lon(idm,jdm),lat(idm,jdm))

lon(1,1) = lon0
lat(1,1) = lat0

j = 1
DO i=2,idm
  lon(i,j) = lon(i-1,j)+dlon
ENDDO

DO j=2,jdm
  lon(:,j) = lon(:,1)
ENDDO

i = 1
DO j=2,jdm
  lat(i,j) = lat(i,j-1)+dlat
ENDDO

DO i=2,idm
  lat(i,:) = lat(1,:)
ENDDO

WHERE(lon>180)lon = lon-360.

!=======================================================================

! 3) Generate new umwm.grid file:
WRITE(*,*)'umwm_gridgen: Writing umwm.grid file.'

CALL nc_check(nf90_create('umwm.grid',nf90_clobber,ncid))
CALL nc_check(nf90_def_dim(ncid,'x',idm,xdimid))
CALL nc_check(nf90_def_dim(ncid,'y',jdm,ydimid))
CALL nc_check(nf90_def_var(ncid,'lon',nf90_float,[xdimid,ydimid],lonid))
CALL nc_check(nf90_def_var(ncid,'lat',nf90_float,[xdimid,ydimid],latid))
CALL nc_check(nf90_put_att(ncid,NF90_GLOBAL,name='history',&
                           values='Generated by umwm_gridgen'))
CALL nc_check(nf90_enddef(ncid))
CALL nc_check(nf90_put_var(ncid,lonid,lon))
CALL nc_check(nf90_put_var(ncid,latid,lat))
CALL nc_check(nf90_close(ncid))

DEALLOCATE(lon,lat)

WRITE(*,*)'umwm_gridgen: Success.'

!=======================================================================
ENDPROGRAM umwm_gridgen
